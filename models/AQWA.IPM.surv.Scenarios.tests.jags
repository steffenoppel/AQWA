
model {

#----------------------------------------
# 1. Define the priors for the parameters
#----------------------------------------

# Initial population sizes

for(a in 1:2){
N1[a,1] ~ dnorm(60, 0.0001)T(0,)           # 1-year old individuals
NadSurv[a,1] ~ dnorm(100, 0.0001)T(0,)      # Adults >= 2 years
}
# DEMOGRAPHIC PARAMETERS INFORMED BY DATA FROM OTHER STUDIES
mfec1 ~ dnorm(3.15,1/(0.16^2))			   ### fecundity = number of fledglings raised per FIRST brood
mfec2 ~ dnorm(2.75,1/(0.2^2))			   ### fecundity = number of fledglings raised per SECOND brood, should be slightly lower than first brood
prop.males ~ dnorm(0.56, 1/(0.01^2))T(0,1)  ### proportion of population that is male and can therefore be counted in population - 56%


# SURVIVAL PRIORS FOR AGE AND SEX GROUPS

#Probably the prior was too informative, let's see with something weakly informative

mphi[2] ~ dbeta(1.2, 1.2)			### survival of adult birds
mphi[1] ~ dbeta(1.2, 1.2)		### survival of first year birds
      for (i in 1:n.marked){
        for (t in f[i]:(n.markocc-1)){
          phi[i,t] <- mphi[age[i,t]]
          p[i,t] <- mean.p[sex[i]]
        } #t
        p[i,n.markocc] <- mean.p[sex[i]]
      } #i
      
      for(sx in 1:2) {
        mean.p[sx] ~ dbeta(1.2, 1.2)                  # resighting probability differs between sexes
      }


#--------------------------------------------------
# 2. Random variation in annual survival and productivity
#--------------------------------------------------
# CHANGE THE SCALE OF DEMOGRAPHIC PARAMETERS TO FACILITATE INCORPORATION OF COVARIATES
l.mphij<-log(mphi[1]/(1-mphi[1]))	    # juvenile survival probability on logit scale;
l.mphia<-log(mphi[2]/(1-mphi[2]))			# adult survival probability on logit scale
log.mfec1 <- log(mfec1)            #first-brood productivity on log scale
log.mfec2 <- log(mfec2)           #second-brood productivity on log scale

# RANDOM ANNUAL EFFECTS ON SURVIVAL AND PRODUCTIVITY

tau.surv<-1/pow(sigma.surv,2)
sigma.surv~dunif(0,5)

tau.prod <- pow(sigma.prod, -2)
sigma.prod ~ dunif(0,2)

for (t in 1:ncountyears){
   eps.surv[t] ~ dnorm(0, tau.surv)						# random variation around annual survival
   eps.fec[t] ~ dnorm(0, tau.prod)            # random variation around productivity
   logit(phij[t]) <- l.mphij+ eps.surv[t]			# add random effect - could add environmental predictors here (if we had any)
   logit(phia[t]) <- l.mphia+ eps.surv[t]			# add random effect - could add environmental predictors here (if we had any) 
   log(fec1[t]) <- log.mfec1 + eps.fec[t]     # add random effect
}


#Fec 2. May activate and deactivate it depending on the scenario. Scenario 1 is there are second broods, Scenario 2 is there aren't any second broods.

  for(t in 1:ncountyears){

   log(fec2[1,t]) <- log.mfec2 + eps.fec[t]
   fec2[2,t] <- 0

  }
  
  #Proportion of second broods
  
  db ~ dnorm(0.25,1/(0.07^2))T(0,1)
  
  
#-----------------------
# 3. Derived parameters
#-----------------------

# Population growth rate
for (t in 1:(ncountyears-1)){
   lambda[t] <- Ntot[1,t+1] / max(1,Ntot[1,t]) # prevent invalid parent error when Ntot=0
   loglambda[t]<-log(lambda[t])## for calculating geometric mean of overall population growth rate
}
    #### OVERALL POPULATION GROWTH RATE  #########
    mean.lambda<-exp((1/(ncountyears-1))*sum(loglambda[1:(ncountyears-1)]))   # Geometric mean

  #Likelihood of the survival process

   # 4.3 Survival estimation
   for (i in 1:n.marked){
    # Define latent state at first capture
    z[i,f[i]] <- 1
    for (t in (f[i]+1):n.markocc){
      # State process
      z[i,t] ~ dbern(z[i,t-1] * phi[i,t-1])
      # Observation process
      y.marked[i,t] ~ dbern(z[i,t] * p[i,t])
    } #t
  } #i
   
   
   #WRITE TWO MODELS HERE! ONE WITH AND ONE WITHOUT SECOND BROODS, NO COUNT CONNECTION
      
      # 4. Likelihood for population count data
#--------------------------------------------
   # 4.1 System process
   
   for(a in 1:2){
   
   #Two scenarios: one with second broods (i.e., no mowing), the other without second broods (i.e., with mowing)
   
   for (t in 2:ncountyears){
        
      	chicks[a,t-1] <- (Ntot[a,t-1])*(1-prop.males)* (fec1[t-1] + fec2[a,t-1]*db) # total fecundity
      	chicksrd[a,t-1] <- round(chicks[a,t-1])
		N1[a,t] ~ dbin(phij[t-1],chicksrd[a,t-1]) 
		NadSurv[a,t] ~ dbin(phia[t-1],round((Ntot[a,t-1])))
	}

   # 4.2 Observation process
   for (t in 1:ncountyears){
   	    Ntot[a,t] <- NadSurv[a,t] + N1[a,t]								## total population		
	      Ntotobs[a,t] <- max(1,(Ntot[a,t]))*prop.males								## only males are counted, which is 56% of population
	      Ntotrd[a,t] <- round(Ntotobs[a,t])
	      
   }
  }
 

}

