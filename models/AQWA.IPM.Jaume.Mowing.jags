
model {


#----------------------------------------
# 1. Define the priors for the parameters
#----------------------------------------

# Initial population sizes
N1[1] ~ dnorm(60, 0.0001)T(0,)           # 1-year old individuals
NadSurv[1] ~ dnorm(100, 0.0001)T(0,)      # Adults >= 2 years

# DEMOGRAPHIC PARAMETERS INFORMED BY DATA FROM OTHER STUDIES
mphia ~ dnorm(0.42,1/(0.03^2))T(0,1)			### survival of adult females
mphij ~ dnorm(0.32,1/(0.025^2))T(0,1)				### survival of first year females
mfec1 ~ dnorm(3.2,1/(0.16^2))			   ### fecundity = number of fledglings raised per FIRST brood
mfec2 ~ dnorm(1.75,1/(0.15^2))			   ### fecundity = number of fledglings raised per SECOND brood, should be slightly lower than first brood
prop.males ~ dnorm(0.56, 1/(0.01^2))T(0,1)  ### proportion of population that is male and can therefore be counted in population - 56%


#--------------------------------------------------
# 2. Random variation in annual survival and productivity
#--------------------------------------------------
# CHANGE THE SCALE OF DEMOGRAPHIC PARAMETERS TO FACILITATE INCORPORATION OF COVARIATES
l.mphij<-log(mphij/(1-mphij))	    # juvenile survival probability on logit scale;
l.mphia<-log(mphia/(1-mphia))			# adult survival probability on logit scale
log.mfec1 <- log(mfec1)            #first-brood productivity on log scale
log.mfec2 <- log(mfec2)           #second-brood productivity on log scale

# RANDOM ANNUAL EFFECTS ON SURVIVAL AND PRODUCTIVITY

tau.surv<-1/pow(sigma.surv,2)
sigma.surv~dunif(0,5)

tau.prod <- pow(sigma.prod, -2)
sigma.prod ~ dunif(0,2)

for (t in 1:(ncountyears+nintroyears)){
   eps.surv[t] ~ dnorm(0, tau.surv)						# random variation around annual survival
   eps.fec[t] ~ dnorm(0, tau.prod)            # random variation around productivity
   logit(phij[t]) <- l.mphij+ eps.surv[t]			# add random effect - could add environmental predictors here (if we had any)
   logit(phia[t]) <- l.mphia+ eps.surv[t]			# add random effect - could add environmental predictors here (if we had any) 
   log(fec1[t]) <- log.mfec1 + eps.fec[t]     # add random effect
   log(fec2[t]) <- log.mfec2 + eps.fec[t]     # add random effect
}


#-----------------------
# 3. Derived parameters
#-----------------------

# Population growth rate
for (t in 1:(ncountyears-1)){
   lambda[t] <- Ntot[t+1] / max(1,Ntot[t]) # prevent invalid parent error when Ntot=0
   loglambda[t]<-log(lambda[t])## for calculating geometric mean of overall population growth rate
}
    #### OVERALL POPULATION GROWTH RATE  #########
    mean.lambda<-exp((1/(ncountyears-1))*sum(loglambda[1:(ncountyears-1)]))   # Geometric mean


#--------------------------------------------
# 4. Likelihood for population count data
#--------------------------------------------
   # 4.1 System process
   for (t in 2:ncountyears){
        
      	chicks[t-1] <- (Ntot[t-1])*(1-prop.males)* fec1[t-1] # total fecundity
      	chicksrd[t-1] <- round(chicks[t-1])
		N1[t] ~ dbin(phij[t-1],chicksrd[t-1]) 
		NadSurv[t] ~ dbin(phia[t-1],round((Ntot[t-1])))
	}

   # 4.2 Observation process
   for (t in 1:ncountyears){
   	    Ntot[t] <- NadSurv[t] + N1[t]								## total population		
	      Ntotobs[t] <- max(1,(Ntot[t]))*prop.males								## only males are counted, which is 56% of population
	      Ntotrd[t] <- round(Ntotobs[t])
        y[t] ~ dpois(Ntotrd[t])
        #  y[t] ~ dbin(prop.males,round(Ntot[t]))                 ## changed from a Poisson to a binomial draw where 56% of the population can be recorded as males - results in inconsistent node error
   }
      
      
# -------------------------------------------------        
# 5. PREDICTION INTO THE FUTURE WITH ONGOING RELEASES
# -------------------------------------------------
### POTENTIALLY INCLUDE SCENARIOS FOR N CAPTIVE BIRDS AND NUMBER OF YEARS
    
    #Set yearly double brood probabilities. Make db and Nfemdb vectors the following length as the others, then it is easier to incorporate them into the PVA.
    
    db[1:(ncountyears-1)] <- 0
    Nfemdb[1:(ncountyears-1)] <- 0
    
    #for (ncr in 1:scen.capt.release){
    CAPT.ADD[ncountyears] <-48   ## 48 were released in first year

      # SPECIFY IMPROVEMENT OF SURVIVAL
      #for (is in 1:scen.imp.surv){ 
    
        # improve.surv ~ dunif(1,1.10)
        # fut.survival[ncr,is] <-imp.surv[is]*mean(ann.surv.terrvis[1:nyears.terrvis])

        ## POPULATION PROCESS
        
        for (t in (ncountyears+1):(ncountyears+nintroyears)){
        
            # CAPTIVE RELEASE OF JUVENILE BIRDS

            CAPT.ADD[t] ~ dunif(44.51,50.49)   ## randomly draw a number for each year. Changed to allow equal probabilities to all numbers. Otherwise we could use a categorical, it would probably be more beautiful
            
            #Double broods start in the future now, adapt the index of the parameters to it.
            
            db[t-1] ~ dnorm(0.25,1/(0.05^2))T(0,1)   ### proportion of double-brooding females 						
            Nfemdb[t-1]  ~ dbin(db[t-ncountyears],round((Ntot[t-1])*0.44))  ## random draw of double brooding females
            
      	    chicks[t-1] <- (Ntot[t-1])*0.44* fec1[t] + Nfemdb[t-1]*fec2[t]			# total fecundity
      	    chicksrd[t-1] <- round(chicks[t-1]) + round(CAPT.ADD[t-1])
		        N1[t] ~ dbin(phij[t-1],chicksrd[t-1]) 
		        NadSurv[t] ~ dbin(phia[t-1],round((Ntot[t-1])))
		   	    Ntot[t] <- NadSurv[t] + N1[t]								## total population	
	}


    #  } # end scenario of imp.surv

    #} # end scenario of n capt released
      
      
      
      
      
}

