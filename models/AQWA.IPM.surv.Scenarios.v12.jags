
model {

#----------------------------------------
# 1. Define the priors for the parameters
#----------------------------------------

# Initial population sizes

N1[1,1] ~ dnorm(60, 0.0001)T(0,)           # 1-year old individuals
NadSurv[1,1] ~ dnorm(100, 0.0001)T(0,)      # Adults >= 2 years

# DEMOGRAPHIC PARAMETERS INFORMED BY DATA FROM OTHER STUDIES
mfec1 ~ dnorm(3.15,1/(0.16^2))			   ### fecundity = number of fledglings raised per FIRST brood
mfec2 ~ dnorm(2.75,1/(0.2^2))			   ### fecundity = number of fledglings raised per SECOND brood, should be slightly lower than first brood
prop.males ~ dnorm(0.56, 1/(0.01^2))T(0,1)  ### proportion of population that is male and can therefore be counted in population - 56%


# SURVIVAL PRIORS FOR AGE AND SEX GROUPS

phi.ad ~ dnorm(0.385,1/(0.04^2))  ## adult femae survival for population projections based on info from Poland

#Juvenile survival by sex and age

mphi[1,1] ~	dbeta(1.2,1.2)	### survival of first year birds - males (not considered)
mphi[1,2] ~ dbeta(1.2,1.2)	### survival of first year birds - females (used as juvenile survival)
mphi[2,1] ~ dbeta(1.2,1.2)  #survival of adults by sex - not considered, we're using a prior
mphi[2,2] ~ dbeta(1.2,1.2)

      for (i in 1:n.marked){
        for (t in f[i]:(n.markocc-1)){
          phi[i,t] <- mphi[age[i,t], sex[i]]
          p[i,t] <- mean.p[sex[i]]
        } #t
        p[i,n.markocc] <- mean.p[sex[i]]
      } #i
      
# PRIORS FOR RESIGHTING PROBABILITY BASED ON SE POLAND
mean.p[1] ~ dnorm(0.57, 0.04)                  # resighting probability for males
mean.p[2] ~ dnorm(0.26, 0.04)T(0,)            # resighting probability for females



#--------------------------------------------------
# 2. Random variation in annual survival and productivity
#--------------------------------------------------

for (t in 1:(ncountyears+nprojyears)){

   phij[t] <- mphi[1,2]			# fixed effect for every occasion - no temporal variability
   phia[t] <- phi.ad			# fixed effect for every occasion - no temporal variability
   fec1[t] <-mfec1
}

#Fec 2, the fecundity of second broods, is only applied to future scenarios now

for(t in 1:nprojyears){
  fec2[t]<-mfec2
}

#-----------------------
# 3. Derived parameters
#-----------------------

# Population growth rate
for (t in 1:(ncountyears-1)){
   lambda[t] <- Ntot[1,t+1] / max(1,Ntot[1,t]) # prevent invalid parent error when Ntot=0
   loglambda[t]<-log(lambda[t])## for calculating geometric mean of overall population growth rate
}
    #### OVERALL POPULATION GROWTH RATE  #########
    mean.lambda<-exp((1/(ncountyears-1))*sum(loglambda[1:(ncountyears-1)]))   # Geometric mean


#--------------------------------------------
# 4. Likelihood for population count data
#--------------------------------------------
   # 4.1 System process
   for (t in 2:ncountyears){
        
      	chicks[1,t-1] <- (Ntot[1,t-1])*(1-prop.males)* fec1[t-1] # total fecundity
      	chicksrd[1,t-1] <- round(chicks[1,t-1])
		N1[1,t] ~ dbin(phij[t-1],chicksrd[1,t-1]) 
		NadSurv[1,t] ~ dbin(phi.ad,round((Ntot[1,t-1])))
	}

   # 4.2 Observation process
   for (t in 1:ncountyears){
   	    Ntot[1,t] <- NadSurv[1,t] + N1[1,t]								## total population		
	      Ntotobs[t] <- max(1,(Ntot[1,t]))*prop.males								## only males are counted, which is 56% of population
	      Ntotrd[t] <- round(Ntotobs[t])
        y[t] ~ dpois(Ntotrd[t])
        
   }
   
   # 4.3 Survival estimation
   for (i in 1:n.marked){
    # Define latent state at first capture
    z[i,f[i]] <- 1
    for (t in (f[i]+1):n.markocc){
      # State process
      z[i,t] ~ dbern(z[i,t-1] * phi[i,t-1])
      # Observation process
      y.marked[i,t] ~ dbern(z[i,t] * p[i,t])
    } #t
  } #i
   
      
  #To make calculations easy, let's replicate the calculations for the past into the 8 scenarios for all objects that are involved as well into the future projections
  # ## TWO SCENARIOS SHOULD PROJECT HISTORIC TREND AND THUS NEED TO START AT HISTORIC POP SIZE
  # COULD NOT WORK OUT HOW TO FIX THIS - cannot define node twice and also cannot leave gaps in matrix
  # cumbersome long manual specification
  
  for(ns in 2:18){
  
    Ntot[ns,1:ncountyears] <- Ntot[1,1:ncountyears]
    N1[ns,1:ncountyears] <- N1[1,1:ncountyears]
    NadSurv[ns,1:ncountyears] <- NadSurv[1,1:ncountyears]
    chicks[ns,1:(ncountyears-1)] <- chicks[1,1:(ncountyears-1)]
    chicksrd[ns,1:(ncountyears-1)] <- chicksrd[1,1:(ncountyears-1)]

  }
  for(t in 2:ncountyears){
    Ntot[19,t] <- Ntot[1,1]
    N1[19,t] <- N1[1,1]
    NadSurv[19,t] <- NadSurv[1,1]
    chicks[19,(t-1)] <- chicks[1,1]
    chicksrd[19,(t-1)] <- chicksrd[1,1]
    Ntot[20,t] <- Ntot[1,1]
    N1[20,t] <- N1[1,1]
    NadSurv[20,t] <- NadSurv[1,1]
    chicks[20,(t-1)] <- chicks[1,1]
    chicksrd[20,(t-1)] <- chicksrd[1,1]
  }
  
  for(ns in 21:(nscenarios-2)){
  
    Ntot[ns,1:ncountyears] <- Ntot[1,1:ncountyears]
    N1[ns,1:ncountyears] <- N1[1,1:ncountyears]
    NadSurv[ns,1:ncountyears] <- NadSurv[1,1:ncountyears]
    chicks[ns,1:(ncountyears-1)] <- chicks[1,1:(ncountyears-1)]
    chicksrd[ns,1:(ncountyears-1)] <- chicksrd[1,1:(ncountyears-1)]

  }
  
  for(t in 2:ncountyears){
    Ntot[39,t] <- Ntot[1,1]
    N1[39,t] <- N1[1,1]
    NadSurv[39,t] <- NadSurv[1,1]
    chicks[39,(t-1)] <- chicks[1,1]
    chicksrd[39,(t-1)] <- chicksrd[1,1]
    Ntot[40,t] <- Ntot[1,1]
    N1[40,t] <- N1[1,1]
    NadSurv[40,t] <- NadSurv[1,1]
    chicks[40,(t-1)] <- chicks[1,1]
    chicksrd[40,(t-1)] <- chicksrd[1,1]
  }
      
# -------------------------------------------------        
# 5. PREDICTION INTO THE FUTURE WITH ONGOING RELEASES
# -------------------------------------------------

#Scenarios to be represented

#1) Mowing (habitat unconstrained) + 5 release years
#2) Mowing (habitat unconstrained) + 10 release years
#3) Mowing (habitat unconstrained) + 15 release years

#4) No mowing (habitat unconstrained) + 5 release years
#5) No mowing (habitat unconstrained) + 10 release years
#6) No mowing (habitat unconstrained) + 15 release years

#7) No mowing + habitat constrained 200ha + 5 release years
#8) No mowing + habitat constrained 200ha + 10 release years
#9) No mowing + habitat constrained 200ha + 15 release years

#10) No mowing + habitat constrained 400ha + 5 release years
#11) No mowing + habitat constrained 400ha + 10 release years
#12) No mowing + habitat constrained 400ha + 15 release years

#13) No mowing + habitat constrained 1200ha + 5 release years
#14) No mowing + habitat constrained 1200ha + 10 release years
#15) No mowing + habitat constrained 1200ha + 15 release years

#16) No mowing + habitat constrained 2400ha + 5 release years
#17) No mowing + habitat constrained 2400ha + 10 release years
#18) No mowing + habitat constrained 2400ha + 15 release years

#19) No mowing (habitat unconstrained) no releases but starting population at N1 (retrospective hypothetical scenario)
#20) Mowing (habitat unconstrained) no releases but starting population at N1 (retrospective hypothetical scenario)


#Hence, there should be variables storing all modifications in parameters involved in these simulations. These are:

#DB and NfemDB should have two dimensions: Nscenario, and time. DB can be manipulated a priori, but not NfemDB

#This should do it for the proportion of double broods

#Scenarios 1 - 3 and 20, no double broods
for(ns in c(1:3,20:23,40)){
  for(t in 1:nprojyears){
  
  db[ns,t] <- 0

  }
}

#The rest, double broods

for(ns in c(4:19,24:39)){
  for(t in 1:nprojyears){
  
  db[ns,t] ~ dnorm(0.25,1/(0.07^2))T(0,1)

  }
}

### INCLUDE SCENARIOS FOR N CAPTIVE BIRDS AND NUMBER OF YEARS
    
    ## POPULATION PROCESS

    for(ns in 1:nscenarios){
      for (t in (ncountyears+1):(ncountyears+nprojyears)){
        
            #Double broods start in the future now, adapt the index of the parameters to it.
            # changed to proportion of actually breeding females AFTER density dependence
            # Nfemdb[ns,t-ncountyears] ~ dbin(db[ns,t-ncountyears],round((Ntot[ns,t-1])*(1-prop.males)))  ## random draw of double brooding females
            
            #Apply density-dependence
            Nfem.breed1[ns,t-ncountyears] <- min(Ntot[ns,t-1]*(1-prop.males), maxrepf[ns])
            Nfem.breed2[ns,t-ncountyears] ~ dbin(db[ns,t-ncountyears],round(Nfem.breed1[ns,t-ncountyears]))  

      	    chicks[ns,t-1] <- Nfem.breed1[ns,t-ncountyears] * fec1[t] + Nfem.breed2[ns,t-ncountyears]*fec2[t-ncountyears]			# total fecundity
      	    chicksrd[ns,t-1] <- round(chicks[ns,t-1]) + releases[ns,t-ncountyears]
		        N1[ns,t] ~ dbin(min(1,phij[t-1]*impsurv[ns]),chicksrd[ns,t-1]) 
		        NadSurv[ns,t] ~ dbin(min(1,phi.ad*impsurv[ns]),round((Ntot[ns,t-1])))
		   	    Ntot[ns,t] <- NadSurv[ns,t] + N1[ns,t]								## total population	
  	} #Time loop
  	
  	
  	## FINAL POPULATION GROWTH RATE (steady state after reintroduction ends)
  	# Population growth rate
    for (t in (ncountyears+10):(ncountyears+nprojyears-1)){
      lambda.f[ns,t] <- Ntot[ns,t+1] / max(1,Ntot[ns,t]) # prevent invalid parent error when Ntot=0
      loglambda.f[ns,t]<-log(lambda.f[ns,t])## for calculating geometric mean of overall population growth rate
    }
    #### OVERALL POPULATION GROWTH RATE  #########
    proj.lambda[ns]<-exp((1/10)*sum(loglambda.f[ns,(ncountyears+10):(ncountyears+nprojyears-1)]))   # Geometric mean
  	
  } #Nscenarios loop
}

